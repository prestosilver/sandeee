#include "/libs/inc/libload.eon"
#include "/libs/inc/sys.eon"

fn processArgs() {
    var file = getArg(1);

    if (file == 0) {
       error("Missing File");
    }

    return file;
}

fn toShort(n) {
    var result = @n;
    var adds = n / 256;

    return result & @adds;
}

fn setPixel(tex, x, y, color) {
    var file = open("/fake/gfx/pixel");

    var xs = toShort(x);
    var ys = toShort(y);
    
    write(file, tex & xs & ys & color);

    close(file);

    return 0;
}

fn render(win, tex, w, h) {
    var source = _MakeRect(0, 0, 1024, 1024);
    var win_w = _WindowWidth(win);
    var win_h = _WindowHeight(win);

    var ratio = 1000;

    var width_ratio = win_w * 1000;
    width_ratio = width_ratio / w;
    var height_ratio = win_h * 1000;
    height_ratio = height_ratio / h;

    ratio = width_ratio;
    if (ratio > height_ratio) {
        ratio = height_ratio;
    }

    var target_w = ratio * w;
    target_w = target_w / 1000;
    var target_h = ratio * h;
    target_h = target_h / 1000;

    var target_x = win_w - target_w;
    var target_y = win_h - target_h;

    var dest = _MakeRect(target_x / 2, target_y / 2, target_w, target_h);
    
    _WindowRender(win, tex, dest, source);
    _WindowFlip(win);

    return 0;
}

fn getclick() {
    asm "push \"/fake/inp/mouse\"";
    asm "sys 3";
    asm "copy 0";
    asm "push 100000";
    asm "sys 4";
    asm "copy 1";
    asm "sys 7";
    asm "disc 1";
    asm "ret";
}

fn drawLineLow(texture, ax, ay, bx, by, color) {
    if (bx > 65535) return 0;

    var dx = bx - ax;
    var dy = by - ay;

    var yi = 1;
    if (dy > 65535) {
        yi = 0 - yi;
        dy = 0 - dy;
    }

    var D = 2 * dy - dx;

    var y = ay;
    var x = 0;
    for (x = ax; x < bx; x = x + 1) {
        setPixel(texture, x, y, color);
        if (D < 255) {
            y = y + yi;
            D = D + 2 * (dy - dx);
        } else {
          D = D + 2 * dy;
        }
    }

    return 0;
}

fn drawLineHigh(texture, ax, ay, bx, by, color) {
    if (by > 65535) return 0;

    var dx = bx - ax;
    var dy = by - ay;

    var xi = 1;
    if (dx > 65535) {
        xi = 0 - xi;
        dx = 0 - dx;
    }

    var D = 2 * dx - dy;

    var x = ax;
    var y = 0;
    for (y = ay; y < by; y = y + 1) {
        setPixel(texture, x, y, color);
        if (D < 255) {
            x = x + xi;
            D = D + 2 * (dx - dy);
        } else {
            D = D + 2 * dx;
        }
    }

    return 0;
}

fn abs(v) {
   if (v > 65535) {
       return 0 - v;
   }
   return v;
}

fn drawLine(texture, ax, ay, bx, by, color) {
    if (abs(ay - by) < abs(ax - bx)) {
        if (ax > bx) {
            drawLineLow(texture, bx, by, ax, ay, color);
        } else {
            drawLineLow(texture, ax, ay, bx, by, color);
        }
    } else {
        if (ay > by) {
            drawLineHigh(texture, bx, by, ax, ay, color);
        } else {
            drawLineHigh(texture, ax, ay, bx, by, color);
        }
    }

    return 0;
}

fn processClick(win, texture, w, h, oldx, oldy) {
    var clickData = getclick();
    var btn = @clickData;

    var x = @(clickData + 2);
    var y = @(clickData + 4);

    var xo = @(clickData + 1);
    var yo = @(clickData + 3);

    x = x + xo * 256;
    y = y + yo * 256;

    print("mouse_uv: ");
    print(x);
    print(", ");
    print(y);
    print("\n");

    var win_w = _WindowWidth(win);
    var win_h = _WindowHeight(win);

    var ratio = 1000;

    var width_ratio = win_w * 1000;
    width_ratio = width_ratio / (w * 2);
    var height_ratio = win_h * 1000;
    height_ratio = height_ratio / (h * 2);

    ratio = width_ratio;
    if (ratio > height_ratio) {
        ratio = height_ratio;
    }

    var target_w = ratio * (w * 2);
    target_w = target_w / 1000;
    var target_h = ratio * (h * 2);
    target_h = target_h / 1000;

    var target_x = win_w - target_w;
    var target_y = win_h - target_h;

    target_x = target_x / 2;
    target_y = target_y / 2;

    x = x - target_x;
    x = x * w;
    x = x / (target_w);

    y = y - target_y;
    y = y * h;
    y = y / (target_h);

    if (btn == 0) {
        drawLine(texture, dup(x), dup(y), oldx, oldy, "\x01\x01\x01\xff");
    } else if (btn == 1) {
        drawLine(texture, dup(x), dup(y), oldx, oldy, "\xff\xff\xff\xff");
    } else {
        print("button: ");
        print(btn);
        print("\n");
    }

    oldx = x;
    oldy = y;

    return 1;
}

fn main() {
    setupLibLoad();
    loadLib("/libs/window.ell");
    loadLib("/libs/texture.ell");

    var input = processArgs();
    var window = _WindowCreate();
    var texture = _TextureCreate();
    var content = _TextureRead(input);
    _TextureUpload(content, texture);
    _SetWindowTitle(window, "EEE Image Array Editor" & input);

    var w = _TextureWidth(content);
    var h = _TextureHeight(content);

    var x = 0;
    var y = 0;

    for (0; _WindowOpen(); 1) {
        processClick(window, texture, w, h, x, y);

        render(window, texture, w * 2, h * 2);
    }

    _TextureDestroy(texture);
    _WindowDestroy(window);

    return 0;
}

