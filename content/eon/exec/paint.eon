#include "/libs/inc/libload.eon"
#include "/libs/inc/sys.eon"

var texture;
var window;
var file_name;
var last_button;

fn processArgs() {
    var file = getArg(1);

    return file;
}

fn toShort(n) {
    var result = @n;
    var adds = n / 256;

    return result & @adds;
}

fn setPixel(x, y, color) {
    var file = open("/fake/gfx/pixel");

    var xs = toShort(x);
    var ys = toShort(y);

    write(file, ($texture) & xs & ys & color);

    close(file);
    
    file = open("/fake/gfx/upload");
    write(file, $texture);
    close(file);

    return 0;
}

fn setSquare(x, y, r, color) {
    var px;
    var py;

    var stopx = x + r;    
    var stopy = y + r;    

    var file = open("/fake/gfx/pixel");
    write(file, $texture);

    for (px = dup(x); px < stopx; px = px + 1) {
        for (py = dup(y); py < stopy; py = py + 1) {
            write(file, toShort(px) & toShort(py) & color);
        }
    }

    close(file);
    
    return 0;
}

fn render(menuTex, iconTex, ttex, imgx, imgy, imgs, w, h) {
    var source = _MakeRect(0, 0, 1024, 1024);
    
    var xo = imgx * imgs;
    xo = xo / 1000;
    var yo = imgy * imgs;
    yo = yo / 1000;

    var target_w = imgs * w;
    target_w = target_w / 1000;

    var target_h = imgs * h;
    target_h = target_h / 1000;

    var dest = _MakeRect(xo, yo + 40, target_w, target_h);

    _WindowRender($window, ttex, dest, _MakeRect(0, 0, 64 * w, 64 * h));
    
    _DrawBarBg($window, menuTex);
    _DrawBarIcon($window, iconTex, _MakeRect(128, 0, 128, 128), 0);
    _DrawBarIcon($window, iconTex, _MakeRect(0, 0, 128, 128), 1);
    _DrawBarIcon($window, iconTex, _MakeRect(256, 0, 128, 128), 2);

    _WindowText($window, 124, 4, "BRUSH_SIZE: 3");

    _WindowRender($window, $texture, dest, source);

    _WindowFlip($window);

    return 0;
}

fn save(tex, target) {
    _TextureExport(tex, target);

    print("saved as " & target & "\n");

    return 0;
}

fn getclick() {
    asm "push \"/fake/inp/mouse\"";
    asm "sys 3";
    asm "copy 0";
    asm "push 100000";
    asm "sys 4";
    asm "copy 1";
    asm "sys 7";
    asm "disc 1";
    asm "ret";
}

fn drawLineLow(ax, ay, bx, by, color) {
    if (bx > 65535) return 0;

    var dx = bx - ax;
    var dy = by - ay;

    var yi = 1;
    if (dy > 65535) {
        yi = -yi;
        dy = -dy;
    }

    var D = 2 * dy - dx;

    var y = dup(ay);
    var x = 0;
    for (x = dup(ax); x < bx; x = x + 1) {
        setSquare(x - 1, y - 1, 3, color);
        if (D < 255) {
            y = y + yi;
            D = D + 2 * (dy - dx);
        } else {
          D = D + 2 * dy;
        }
    }

    return 0;
}

fn drawLineHigh(ax, ay, bx, by, color) {
    if (by > 65535) return 0;

    var dx = bx - ax;
    var dy = by - ay;

    var xi = 1;
    if (dx > 65535) {
        xi = -xi;
        dx = -dx;
    }

    var D = 2 * dx - dy;

    var x = dup(ax);
    var y = 0;
    for (y = dup(ay); y < by; y = y + 1) {
        setSquare(x - 1, y - 1, 3, color);
        if (D < 255) {
            x = x + xi;
            D = D + 2 * (dx - dy);
        } else {
            D = D + 2 * dx;
        }
    }

    return 0;
}

fn abs(v) {
   if (v > 65535) {
       return -v;
   }
   return v;
}

fn drawLine(ax, ay, bx, by, color) {
    if (abs(ay - by) < abs(ax - bx)) {
        if (ax > bx) {
            drawLineLow(bx, by, ax, ay, color);
        } else {
            drawLineLow(ax, ay, bx, by, color);
        }
    } else {
        if (ay > by) {
            drawLineHigh(bx, by, ax, ay, color);
        } else {
            drawLineHigh(ax, ay, bx, by, color);
        }
    }
    
    var file = open("/fake/gfx/upload");
    write(file, $texture);
    close(file);

    return 0;
}

fn processClick(imgx, imgy, imgs, w, h, oldx, oldy) {
    var clickData = getclick();
    var btn = @clickData;

    var x = @(clickData + 2);
    var y = @(clickData + 4);

    var xo = @(clickData + 1);
    var yo = @(clickData + 3);

    x += xo * 256;
    y += yo * 256;

    if ((y < 40) && (btn == 0)) {
        if (x < 44) {
            return 0;
        }
        if (x < 84) {
            save($texture, $file_name);

            return 0;
        }

        print("Unknown click ");
        print(x);
        print(", ");
        print(y);
        print("\n");

        return 0;
    }

    y -= 40;

    var xs = dup(x);
    var ys = dup(y);

    xo = imgx * imgs;
    xo = xo / 1000;
    x -= xo;
    x = x * 1000;
    x = x / imgs;

    yo = imgy * imgs;
    yo = yo / 1000;
    y -= yo;
    y = y * 1000;
    y = y / imgs;

    xo = oldx - x;
    yo = oldy - y;

    if (btn == 0) {
        drawLine(oldx, oldy, x, y, "\x01\x01\x01\xff");
    } else if (btn == 1) {
        drawLine(oldx, oldy, x, y, "\xff\xff\xff\xff");
    } else if (btn == 2) {
        if ((imgx - xo) < 65535) {
            imgx -= xo;
        }

        if ((imgy - yo) < 65535) {
            imgy -= yo;
        }

        x = xs;
        y = ys;

        xo = imgx * imgs;
        xo = xo / 1000;
        x -= xo;
        x = x * 1000;
        x = x / imgs;

        yo = imgy * imgs;
        yo = yo / 1000;
        y -= yo;
        y = y * 1000;
        y = y / imgs;
    }

    oldx = dup(x);
    oldy = dup(y);

    return 1;
}

fn textureNew(w, h) {
    return "eimg" & toShort(w) & toShort(h) & (new (w * h * 4));
}

fn main() {
    setupLibLoad();
    loadLib("/libs/window.ell");
    loadLib("/libs/texture.ell");
    loadLib("/libs/ui.ell");

    file_name := processArgs();
    window := _WindowCreate();
    texture := _TextureCreate();

    var menuTex = _TextureCreate();
    var iconTex = _TextureCreate();
    var transTex = _TextureCreate();
    _TextureUpload(_TextureRead("/cont/imgs/transparent.eia"), transTex);
    _TextureUpload(_TextureRead("/cont/imgs/ui.eia"), menuTex);
    _TextureUpload(_TextureRead("/cont/imgs/icons.eia"), iconTex);

    var w = 0;
    var h = 0;
    
    var cont = "";
    if (($file_name) != "") {
        cont = _TextureRead($file_name);
    } else {
        file_name := "image.eia";
        cont = textureNew(150, 150);
    }

    _TextureUpload(cont, $texture);
    w = _TextureWidth(cont);
    h = _TextureHeight(cont);

    _SetWindowTitle($window, "EEE Image Array Editor - " & $file_name);

    var x = 0;
    var y = 0;

    var imgx = 0;
    var imgy = 0;
    var imgs = 2000;

    while (_WindowOpen()) {
        processClick(imgx, imgy, imgs, w, h, x, y);

        render(menuTex, iconTex, transTex, imgx, imgy, imgs, w, h);
    }

    _TextureDestroy(menuTex);
    _TextureDestroy($texture);
    _WindowDestroy($window);

    return 0;
}
