#include "/libs/inc/libload.eon"
#include "/libs/inc/sys.eon"

fn render(win, tex, ax, bx,
          ballx, bally, vx, vy) {
    var source = "";
    var dest = "";

    source = _MakeRect(256, 0, 256, 1024);
    dest = _MakeRect(0, 0, 400, 300);

    _WindowRender(win, tex, dest, source);

    source = _MakeRect(0, 0, 256, 1024);
    dest = _MakeRect(ballx, bally, 10, 10);

    _WindowRender(win, tex, dest, source);

    source = _MakeRect(0, 0, 256, 1024);
    dest = _MakeRect(10, ax, 20, 100);

    _WindowRender(win, tex, dest, source);

    source = _MakeRect(0, 0, 256, 1024);
    dest = _MakeRect(370, bx, 20, 100);

    _WindowRender(win, tex, dest, source);

    _WindowText(win, 150, 10, "0 - 0");

    return 0;
}

fn getkey() {
    asm "push \"/fake/inp/win\"";
    asm "sys 3";
    asm "copy 0";
    asm "push 100000";
    asm "sys 4";
    asm "copy 1";
    asm "sys 7";
    asm "disc 1";
    asm "ret";
}

fn update(ax, bx, ballx, bally,
          vx, vy, pings, blip, dt) {
    var tvx;
    var tvy;
    var contained;
    var tmp;

    var acc = pings / 5 + 1;

    if (vx < 100) {
        tvx = 100 - vx;
        tvx = acc * tvx / 12;

        contained = bally < ax + 100;
        tmp = bally > ax;
        contained = contained && tmp;
        tmp = ballx < tvx + 30;
        contained = contained && tmp;

        if (contained) {
            _playSound(blip);
            vx = 200 - vx;
            ballx = ballx + tvx;
            vy = bally - ax;
            vy = vy * 2;
            pings = pings + 1;
        } else {
            if (ballx < tvx) {
                vx = 200;
                vy = 0;
                ballx = 195;
                bally = 145;
                pings = 1;
                return 0;
            }
            ballx = ballx - tvx;
        }
    } else {
        tvx = vx - 100;
        tvx = acc * tvx / 12;

        contained = bally < bx + 100;
        tmp = bally > bx;
        contained = contained && tmp;
        tmp = ballx > 360 - tvx;
        contained = contained && tmp;

        if (contained) {
            _playSound(blip);
            vx = 200 - vx;
            vy = bally - bx;
            vy = vy * 2;
            ballx = ballx - tvx;
            pings = pings + 1;
        } else {
            if (ballx > 390 - tvx) {
                vx = 0;
                vy = 200;
                ballx = 195;
                bally = 145;
                pings = 1;
                return 0;
            }
            ballx = ballx + tvx;
        }
    }

    if (vy < 100) {
        tvy = 100 - vy;
        tvy = tvy / 12;

        if (bally < tvy) {
            vy = 200 - vy;
            _playSound(blip);
            bally = bally + tvy;
        } else {
            bally = bally - tvy;
        }
    } else {
        tvy = vy - 100;
        tvy = tvy / 12;
        
        if (bally > 290 - tvy) {
            bally = bally - tvy;
            vy = 200 - vy;
            _playSound(blip);
        } else {
            bally = bally + tvy;
        }
    }

    var keycode = 0;
    var adds = 0;
    
    for (var key = getkey(); _StringLength(key) != 0; key = key + 2) {
        keycode = @dup(key);
        adds = key + 1;
        adds = @adds;
        adds = adds * 256;
        keycode = keycode + adds;

        if (keycode == 87) {
            if (ax > 9) ax = ax - 10;
        } else if (keycode == 83) {
            if (ax < 200) ax = ax + 10;
        } else if (keycode == 265) {
            if (bx > 9) bx = bx - 10;
        } else if (keycode == 264) {
            if (bx < 200) bx = bx + 10;
        }
    }

    return 0;
}

fn main() {
    setupLibLoad();
    loadLib("/libs/window.ell");
    loadLib("/libs/texture.ell");
    loadLib("/libs/string.ell");
    loadLib("/libs/sound.ell");

    var window = _WindowCreate();
    var texture = _TextureCreate();
    var lastTime = time();
    var dt = 0;
    var ballx = 195;
    var bally = 145;
    var ax = 100;
    var bx = 100;
    var vx = 0;
    var vy = 200;
    var pings = 1;
    var blip = readAll("/cont/snds/pong-blip.era");

    _SetWindowRule("min", 408, 338);
    _SetWindowRule("max", 408, 338);
    _SetWindowTitle(window, "Pong");
    _SetWindowSize(408, 338);

    var test = "";

    _TextureUpload(_TextureRead("/cont/imgs/pong.eia"), texture);

    while (_WindowOpen()) {
        dt = time() - lastTime;
        lastTime = time();

        update(ax, bx, ballx, bally, vx, vy, pings, blip, dt);
        
        render(window, texture, ax, bx, ballx, bally, vx, vy);
        
        _WindowFlip(window);
    }

    _TextureDestroy(texture);
    _WindowDestroy(window);

    return 0;
}

