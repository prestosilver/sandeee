#include "/libs/incl/libload.eon"
#include "/libs/incl/sys.eon"

var tex; 

fn processArgs() {
    var file = getArg(1);

    if (file == 0) {
       error("Missing File");
    }

    return file;
}

fn render(win, w, h) {
    _WindowClear(win);

    var source = _MakeRect(0, 0, 1024, 1024);
    var win_w = _WindowWidth(win);
    var win_h = _WindowHeight(win);

    var ratio = 1000;

    var width_ratio = win_w * 1000;
    width_ratio = width_ratio / w;
    var height_ratio = win_h * 1000;
    height_ratio = height_ratio / h;

    ratio = width_ratio;
    if (ratio > height_ratio) {
        ratio = height_ratio;
    }

    var target_w = ratio * w;
    target_w = target_w / 1000;
    var target_h = ratio * h;
    target_h = target_h / 1000;

    var target_x = win_w - target_w;
    var target_y = win_h - target_h;

    var dest = _MakeRect(target_x / 2, target_y / 2, target_w, target_h);
    
    _WindowRender(win, $tex, dest, source);
    _WindowFlip(win);

    return 0;
}

fn main() {
    setupLibLoad();
    loadLib("/libs/window.ell");
    loadLib("/libs/texture.ell");

    var input = processArgs();
    var window = _WindowCreate();
    var content = _TextureRead(input);
    
    tex := _TextureCreate();

    _TextureUpload(content, $tex);

    _SetWindowTitle(window, "PixEEE - " & input);

    var w = _TextureWidth(content);
    var h = _TextureHeight(content);

    while (_WindowOpen()) {
        render(window, w * 2, h * 2);
    }

    _TextureDestroy($tex);
    _WindowDestroy(window);

    return 0;
}

