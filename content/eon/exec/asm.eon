#include "content/eon/libs/libload.eon"
#include "content/eon/libs/table.eon"
#include "content/eon/libs/sys.eon"

void error(value msg) {
    print("Error: " & msg);
    quit();
}

value processArgs() {
    value file = getArg(1);

    if (file == 0) {
       error("Bad Argument");
    }

    return file;
}

value stripLine(value line) {
    value chr = 0;
    for (chr = @line; chr == 32; chr = @line) {
        line = line + 1;
    }

    return line;
}

value getLineEnum(value line) {
    if (_StringStartsWith(line, "nop")) {
        return 0;
    } else if (_StringStartsWith(line, "sys")) {
        return 1;
    } else if (_StringStartsWith(line, "push")) {
        return 2;
    } else if (_StringStartsWith(line, "add")) {
        return 3;
    } else if (_StringStartsWith(line, "sub")) {
        return 4;
    } else if (_StringStartsWith(line, "copy")) {
        return 5;
    // jmpf early bc reasons
    } else if (_StringStartsWith(line, "jmpf")) {
        return 9;
    } else if (_StringStartsWith(line, "jmp")) {
        return 6;
    } else if (_StringStartsWith(line, "jz")) {
        return 7;
    } else if (_StringStartsWith(line, "jnz")) {
        return 8;
    } else if (_StringStartsWith(line, "mul")) {
        return 10;
    } else if (_StringStartsWith(line, "div")) {
        return 11;
    } else if (_StringStartsWith(line, "and")) {
        return 12;
    } else if (_StringStartsWith(line, "or")) {
        return 13;
    } else if (_StringStartsWith(line, "not")) {
        return 14;
    } else if (_StringStartsWith(line, "eq")) {
        return 15;
    } else if (_StringStartsWith(line, "getb")) {
        return 16;
    } else if (_StringStartsWith(line, "ret")) {
        return 17;
    } else if (_StringStartsWith(line, "call")) {
        return 18;
    } else if (_StringStartsWith(line, "neg")) {
        return 19;
    } else if (_StringStartsWith(line, "xor")) {
        return 20;
    } else if (_StringStartsWith(line, "disc")) {
        return 21;
    } else if (_StringStartsWith(line, "set")) {
        return 22;
    } else if (_StringStartsWith(line, "dup")) {
        return 23;
    } else if (_StringStartsWith(line, "lt")) {
        return 24;
    } else if (_StringStartsWith(line, "gt")) {
        return 25;
    } else if (_StringStartsWith(line, "cat")) {
        return 26;
    } else if (_StringStartsWith(line, "mod")) {
        return 27;
    } else if (_StringStartsWith(line, "create")) {
        return 28;
    }

    return 255;
}

value getLineData(value table, value line) {
    value chr = 0;
    for (chr = @line; chr != 32; chr = @line) {
        line = line + 1;
        if (_StringLength(line) == 0) {
            return @0;
        }
    }

    line = line + 1;

    value starts = @line == 34;
    value ends = _StringLast(line) == 34;

    if (starts && ends) {
       line = line + 1;
       line = line - 1;
       return @2 & line & @0;
    }

    value num = _StringToNum(line);

    value name = "";

    for (name = dup(line); _StringLength(name) < 20; 0) {
        name = name & @0;
    }
    
    if (tableGet(table, name)) {
        num = @tableGet(table, name);
    }

    if (num < 256) {
       return @3 & @num;
    }

    return @1 & num;
}

value processLine(value table, value line) {
    value stripped = stripLine(line);
    value enum = getLineEnum(stripped);

    if (enum == 255) {
       error("Invalid Instruction " & stripped);
    }

    value data = getLineData(table, stripped);

    return @enum & data;
}

value processFile(value table, value infile, value outfile) {
    write(outfile, "EEEp");

    value line = "";

    value chr = read(infile, 1);
    for (chr = chr; chr != ""; chr = read(infile, 1)) {
        if (@chr == 92) {
            chr = read(infile, 1);
            if (@chr == 110) {
                line = line & @10;
            } else {
                line = line & chr;
            }
        } else if (@chr == 10) {
            if (_StringLast(line) != 58) {
                write(outfile, processLine(table, line));
            }

            line = "";
        } else {
            line = line & chr;
        }
    }

    return 0;
}

value processConsts(value table, value infile) {
    value line = "";

    value chr = read(infile, 1);
    value name = "";
    value idx = 0;
    for (chr = chr; chr != ""; chr = read(infile, 1)) {
        if (@chr == 92) {
            chr = read(infile, 1);
            if (@chr == 78) {
                line = line & @10;
            } else {
                line = line & chr;
            }
        } else if (@chr == 10) {
            if (_StringLast(line) == 58) {
                for (name = line - 1; _StringLength(name) < 20; 0) {
                    name = name & @0;
                }
                tablePut(table, name, @idx);
            } else {
                idx = idx + 1;
            }

            line = "";
        } else {
            line = line & chr;
        }
    }

    return 0;
}

void main() {
    // load libs
    setupLibLoad();
    loadLib("/libs/string.ell");

    value table = tableCreate(20, 1);

    // open output
    value outfile = "out.eep";
    value outhandle = createOpen(outfile);

    // open input
    value infile = processArgs();
    value inhandlea = open(infile);
    value inhandleb = open(infile);

    // get consts
    print("READ: " & infile & "\\n");
    processConsts(table, inhandlea);
    close(inhandlea);

    // create out file
    print("ASM: " & infile & "\\n");
    processFile(table, inhandleb, outhandle);

    print("Wrote: '" & outfile & "'\\n");

    close(inhandleb);
    close(outhandle);

    asm "ret";
}
