fn print(str) {
    asm "sys 0";
    asm "push 0";
    asm "ret";
}

fn write(file, text) {
    asm "sys 5";
    asm "push 0";
    asm "ret";
}

fn read(file, len) {
    asm "sys 4";
    asm "ret";
}

fn open(path) {
    asm "sys 3";
    asm "ret";
}

fn createOpen(path) {
    asm "dup 0";
    asm "sys 2";
    asm "sys 3";
    asm "ret";
}

fn close(file) {
    asm "sys 7";
    asm "push 0";
    asm "ret";
}

fn stripLine(line) {
    for (var chr = @line; chr == 32; chr = @line) {
        line = line + 1;
    }

    return line;
}

fn getLineEnum(line) {
    if (_StringStartsWith(line, "nop")) {
        return 0;
    } else if (_StringStartsWith(line, "sys")) {
        return 1;
    } else if (_StringStartsWith(line, "push")) {
        return 2;
    } else if (_StringStartsWith(line, "add")) {
        return 3;
    } else if (_StringStartsWith(line, "sub")) {
        return 4;
    } else if (_StringStartsWith(line, "copy")) {
        return 5;
        // jmpf early bc reasons
    } else if (_StringStartsWith(line, "jmpf")) {
        return 9;
    } else if (_StringStartsWith(line, "jmp")) {
        return 6;
    } else if (_StringStartsWith(line, "jz")) {
        return 7;
    } else if (_StringStartsWith(line, "jnz")) {
        return 8;
    } else if (_StringStartsWith(line, "mul")) {
        return 10;
    } else if (_StringStartsWith(line, "div")) {
        return 11;
    } else if (_StringStartsWith(line, "and")) {
        return 12;
    } else if (_StringStartsWith(line, "or")) {
        return 13;
    } else if (_StringStartsWith(line, "not")) {
        return 14;
    } else if (_StringStartsWith(line, "eq")) {
        return 15;
    } else if (_StringStartsWith(line, "getb")) {
        return 16;
    } else if (_StringStartsWith(line, "ret")) {
        return 17;
    } else if (_StringStartsWith(line, "call")) {
        return 18;
    } else if (_StringStartsWith(line, "neg")) {
        return 19;
    } else if (_StringStartsWith(line, "xor")) {
        return 20;
    } else if (_StringStartsWith(line, "disc")) {
        return 21;
    } else if (_StringStartsWith(line, "set")) {
        return 22;
    } else if (_StringStartsWith(line, "dup")) {
        return 23;
    } else if (_StringStartsWith(line, "lt")) {
        return 24;
    } else if (_StringStartsWith(line, "gt")) {
        return 25;
    } else if (_StringStartsWith(line, "cat")) {
        return 26;
    } else if (_StringStartsWith(line, "mod")) {
        return 27;
    } else if (_StringStartsWith(line, "create")) {
        return 28;
    } else if (_StringStartsWith(line, "size")) {
        return 29;
    } else if (_StringStartsWith(line, "len")) {
        return 30;
    } else if (_StringStartsWith(line, "sin")) {
        return 31;
    } else if (_StringStartsWith(line, "cos")) {
        return 32;
    }

    return 255;
}

fn fromEntry(entry) {
   var large = @(entry + 1);

   return (@entry) + large * 256;
}

fn getLineData(table, line) {
    for (var chr = @line; chr != 32; chr = @line) {
        line = line + 1;
        if (_StringLength(line) == 0) {
            return @0;
        }
    }

    line = line + 1;

    var starts = @line == 34;
    var ends = _StringLast(line) == 34;

    if (starts && ends) {
        line = line + 1;
        line = line - 1;
        return @2 & line & @0;
    }

    var num = _StringToNum(line);
    var name = "";
    for (name = dup(line); _StringLength(name) < 20; 0) {
        name = name & @0;
    }

    if (_TableGet(table, name)) {
        num = fromEntry(_TableGet(table, name));
    }

    if (num < 256) {
        return @3 & @num;
    }

    return @1 & num;
}

fn processLine(table, line) {
    var stripped = stripLine(line);

    if (_StringLength(stripped) == 0) return "";
    
    var enum = getLineEnum(stripped);

    if (enum == 255) {
        asm "push \"Invalid Instruction \"";
        asm "dup 2";
        asm "cat";
        asm "sys 18";
    }

    var data = getLineData(table, stripped);

    return @enum & data;
}

fn processFile(totalLines, toc_count, table, infile, outfile, isLib) {
    if (isLib) {
        write(outfile, "elib");
    } else {
        write(outfile, "EEEp");
    }

    var toc = @toc_count; 
    var conts = "";

    var line = "";
    var currentLine = 0;
    var prev_toc = 0;
    var len = 0;

    var done = 0;
    for (var chr = read(infile, 1); done == 0; chr = read(infile, 1)) {
        if (@chr == 92) {
            chr = read(infile, 1);
            if (@chr == 110) {
                line = line & @10;
            } else if (@chr == 114) {
                line = line & @13;
            } else {
                line = line & chr;
            }
        } else if (@chr == 10) {
            if (_StringLast(line) != 58) {
                conts &= processLine(table, line);
            } else {
                if (@line == 95) {
                    line += 1;
                    line -= 1;

                    if (prev_toc != 0) {
                        len = _StringLength(conts) - prev_toc;
                        len += 1;
                        toc &= @(len / 256);
                        toc &= @len;
                    }
                    toc &= @(_StringLength(line));
                    print(line & "\n");
                    toc &= line;
                    toc &= @0;
                    prev_toc = _StringLength(conts) + 1;
                }
            }
                
            currentLine += 1;

            if (currentLine % 5 == 0) {
                print("\r");
                print(currentLine);
                print("/");
                print(totalLines);
            }

            line = "";
        } else if (chr == "") {
            if (_StringLast(line) != 58) {
                conts &= processLine(table, line);
            } else {
                print(line & "\n");

                if (@line == 95) {
                    line += 1;
                    line -= 1;

                    if (prev_toc != 0) {
                        len = _StringLength(conts) - prev_toc + 1;
                        toc &= @(len / 256);
                        toc &= @len;
                    }
                    toc &= @(_StringLength(line));
                    print(line & "\n");
                    toc &= line;
                    toc &= @0;
                    prev_toc = _StringLength(conts) + 1;
                }
            }
                
            print("\r");
            print(currentLine);
            print("/");
            print(totalLines);
            print("\n");

            line = "";
            done = 1;
        } else {
            line = line & chr;
        }
    }

    len = _StringLength(conts) - prev_toc;
    len += 1;
    toc &= @(len / 256);
    toc &= @len;

    if (isLib) {
        len = _StringLength(toc) + 6;

        write(outfile, @(len / 256));
        write(outfile, @len);

        write(outfile, toc);
    }

    write(outfile, conts);

    return 0;
}

fn toEntry(val) {
   return (@val) & @(val / 256);
}

fn processConsts(table, toc_count, infile) {
    var totalLines = 0;
    var line = "";

    var name = "";
    var idx = 0;
    var done = 0;
    for (var chr = read(infile, 1); done == 0; chr = read(infile, 1)) {
        if (@chr == 92) {
            chr = read(infile, 1);
            if (@chr == 78) {
                line = line & @10;
            } else {
                line = line & chr;
            }
        } else if (@chr == 10) {
            if (@line == 95) {
                toc_count += 1;
                idx = 0;
            } else {
                if (_StringLast(line) == 58) {
                    for (name = line - 1; _StringLength(name) < 20; 0) {
                        name = name & @0;
                    }
                    _TablePut(table, name, toEntry(idx));
                } else {
                    idx = idx + 1;
                }
            }

            totalLines = totalLines + 1;
            line = "";
        } else if (chr == "") {
            if (_StringLast(line) == 58) {
                for (name = line - 1; _StringLength(name) < 20; 0) {
                    name &= @0;
                }
                _TablePut(table, name, toEntry(idx));
            } else {
                idx = idx + 1;
            }

            line = "";
            done = 1;
        } else {
            line = line & chr;
        }
    }

    return totalLines;
}

fn libInit() {
    _libload("/libs/table.ell");

    asm "ret";
}

fn asmFile(inname, outname, isLib) {
    var table = _TableCreate(20, 2);

    var inhandleb = open(inname);
    var inhandlea = open(inname);

    var toc_count = 0;

    // open output
    var outhandle = createOpen(outname);

    // get consts
    print("PASS1: " & inname & "\n");
    var totalLines = processConsts(table, toc_count, inhandlea);
    close(inhandlea);

    // create out file
    print("PASS2: " & inname & "\n");
    processFile(totalLines, toc_count, table, inhandleb, outhandle, isLib);

    print("Wrote: '" & outname & "'\n");

    close(inhandleb);
    close(outhandle);

    return 0;
}
